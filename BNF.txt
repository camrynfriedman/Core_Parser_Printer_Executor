(1)     <prog> ::= program <decl seq> begin <stmt seq> end;
(2)     <decl seq> ::= <decl> | <decl> <decl seq>
(3)     <stmt seq> ::= <stmt> | <stmt> <stmt seq>
(4)     <decl> ::= int <id list>
(5)     <id list> ::= <id> | <id>, <id list>
(6)     <stmt> ::= <assign> | <if> | <loop> | <in> | <out>
(7)     <assign> ::= <id> = <exp>;
(8)     <if> ::= if <cond> then <stmt seq> end; | if <cond> then <stmt seq> else <stmt seq> end;
(9)     <loop> ::= while <cond> loop <stmt seq> end;
(10)    <in> ::= read <id list>;
(11)    <out> ::= write <id list>;
(12)    <cond> ::= <comp> | !<cond> | [<cond> && <cond>] | [<cond> or <cond>]
(13)    <comp> ::= (<op> <comp op> <op>)
(14)    <exp> ::= <fac> | <fac> + <exp> | <fac> - <exp>
(15)    <fac> ::= <op> | <op> * <fac>
(16)    <op> ::= <int> | <id> | (<exp>)
(17)    <comp op> ::= != | == | < | > | <= | >=
(18)    <id> ::= ...as defined by RE...
(19)    <int> ::= ...as defined by RE (only unsigned numbers)